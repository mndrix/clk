#!/usr/bin/perl
use strict;
use warnings;
use List::AllUtils qw( none sum uniq );
use WWW::Bugzilla3;

my $password = shift || die "Provide your password";

my %hours;
while (<>) {
    chomp;
    my (undef, $time, $duration, $tags, $msg) = split /\t/;
    next if $msg eq 'out';
    my @tags = split /,/, $tags;
    next if none { $_ eq 'jjgames' } @tags;

    $msg = 'reading server emails' if $msg eq 'email';

    $duration =~ s/s$//;
    $hours{$msg} += $duration/3600;
}

# find the Bug summaries for each bug
my @bug_ids = map { /^b(\d+)$/ ? $1 : () } keys %hours;
my $summary_for = bug_summaries( $password, @bug_ids );

my @lines;
for my $msg ( sort keys %hours ) {
    my $out = $msg;
    $out = "$summary_for->{$1} (bug #$1)" if $msg =~ /^b(\d+)$/;
    push @lines, sprintf("%.1f - $out", $hours{$msg});
}
@lines = reverse sort { num($a) <=> num($b) } @lines;
print "$_\n" for @lines;
printf "Total: %.1f\n", sum values %hours;

exit;

sub num { $_[0] =~ m/([\d.]+)/ ? $1 : 0 }

sub bug_summaries {
    my ( $password, @bug_ids ) = @_;
    my %summary_for;

    # login to bugs.ndrix.com
    my $bz = WWW::Bugzilla3->new( site => 'bugs.ndrix.com' );
    $bz->login( 'michael@ndrix.com', $password );

    # fetch the bug details for all bugs
    my @bugs = $bz->get_bugs( uniq @bug_ids );
    for my $bug (@bugs) {
        $summary_for{ $bug->{id} } = $bug->{summary};
    }

    return \%summary_for;
}
